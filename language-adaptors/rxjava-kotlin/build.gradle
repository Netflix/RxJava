buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://repository.jetbrains.com/all'
        }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:0.5.748'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'osgi'


repositories {
    maven {
        url 'http://repository.jetbrains.com/all'
    }
}

dependencies {
    compile project(':rxjava-core')
    compile 'org.jetbrains.kotlin:kotlin-stdlib:0.5.748'
    provided 'junit:junit-dep:4.10'
    provided 'org.mockito:mockito-core:1.8.5'
    provided 'com.google.guava:guava:14.0.1'
}

eclipse {
    classpath {
        // include 'provided' dependencies on the classpath
        plusConfigurations += configurations.provided

        downloadSources = true
        downloadJavadoc = true
    }
}

idea {
    module {
        // include 'provided' dependencies on the classpath
        scopes.PROVIDED.plus += configurations.provided
    }
}

jar {
    manifest {
        name = 'rxjava-kotlin'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/RxJava'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
        instruction 'Fragment-Host', 'com.netflix.rxjava.core'
    }
}