ext.githubProjectName = 'RxJava'

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
//apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

buildscript {
    repositories {
        mavenLocal()
        mavenCentral() // maven { url 'http://jcenter.bintray.com' }
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    repositories { 
        mavenLocal()
        mavenCentral() // maven { url: 'http://jcenter.bintray.com' }
    }
}

subprojects {
    apply plugin: 'java'
    group = "com.netflix.rxjava"

    // make 'examples' use the same classpath
    configurations { 
        examplesCompile.extendsFrom compile
        examplesRuntime.extendsFrom runtime
	    perfCompile.extendsFrom compile
	    perfRuntime.extendsFrom runtime
    }


    tasks.withType(Javadoc).each {
        it.classpath = sourceSets.main.compileClasspath
    }

    sourceSets { 
		//include /src/examples folder
        examples
		//include /src/perf folder
        perf {
            java {
                srcDir 'src/perf/java'
                compileClasspath += main.output
                runtimeClasspath += main.output
            }
        }
    }

    dependencies {
        perfCompile 'com.google.caliper:caliper:1.0-beta-1'
        perfRuntime 'com.sun.jersey:jersey-core:1.11'
    }

    tasks.build { 
		//include 'examples' in build task
        dependsOn(examplesClasses)
		//include 'perf' in build task
        // dependsOn(perfClasses)  //-> Not working so commented out
    }
    
    eclipse {  
        classpath {  
            // include 'provided' dependencies on the classpath
            plusConfigurations += configurations.provided
            plusConfigurations += configurations.perfCompile
            
            downloadSources = true
            downloadJavadoc = true
        }
    }
    
    idea {
        module {
            // include 'provided' dependencies on the classpath
            scopes.PROVIDED.plus += configurations.provided
            // TODO not sure what to add it to
            //scopes.PROVIDED.plus += configurations.perfCompile
        }
    }
}

project(':rxjava-core') {
    sourceSets.test.java.srcDir 'src/test/java'
}
