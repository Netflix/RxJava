apply plugin: 'maven'
apply plugin: 'osgi'
apply plugin:'application'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {
    provided 'junit:junit-dep:4.10'
    provided 'org.mockito:mockito-core:1.8.5'
}

javadoc {
    // we do not want the org.rx.operations package include
    exclude '**/operations/**'
	
    options {
        doclet = "org.benjchristensen.doclet.DocletExclude"
        docletpath = [rootProject.file('./gradle/doclet-exclude.jar')]
        stylesheetFile = rootProject.file('./gradle/javadocStyleSheet.css')
        windowTitle = "RxJava Javadoc ${project.version}"
    }
    options.addStringOption('top').value = '<h2 class="title" style="padding-top:40px">RxJava</h2>'
}

jar {
    manifest {
        name = 'rxjava-core'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/RxJava'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
    }
}

task time(type:JavaExec) {
    //sourceSets.metaClass.methods.each { println it }
    classpath = sourceSets.perf.runtimeClasspath
    group 'Application'
    description 'Execute the calipser benchmark timing of Rx'
    main 'rx.operators.ObservableBenchmark'
    // args '--print-config' // dump the caliper configuration before starting
    // args '--verbose' // uncomment to have caliper log what it's doing
    args '--trials=1' // only run the experiments once
    //args '--time-limit=0' // experiments never timeout
}