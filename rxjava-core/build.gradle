apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'osgi'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.0'
    provided 'junit:junit:4.10'
    provided 'org.mockito:mockito-core:1.8.5'
}

eclipse {
  classpath {
    // include 'provided' dependencies on the classpath
    plusConfigurations += configurations.provided

    downloadSources = true
    downloadJavadoc = true
  }
}

idea {
	module {
		// include 'provided' dependencies on the classpath
		scopes.PROVIDED.plus += configurations.provided
	}
}

javadoc {
    // we do not want the org.rx.operations package include
    exclude '**/operations/**'
	
    options {
        doclet = "org.benjchristensen.doclet.DocletExclude"
        docletpath = [rootProject.file('./gradle/doclet-exclude.jar')]
        stylesheetFile = rootProject.file('./gradle/javadocStyleSheet.css')
        windowTitle = "RxJava Javadoc ${project.version}"
    }
    options.addStringOption('top').value = '<h2 class="title" style="padding-top:40px">RxJava</h2>'
}

jar {
    manifest {
        name = 'rxjava-core'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/RxJava'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
    }
}

